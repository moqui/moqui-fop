/*
 * This software is in the public domain under CC0 1.0 Universal plus a 
 * Grant of Patent License.
 * 
 * To the extent possible under law, the author(s) have dedicated all
 * copyright and related and neighboring rights to this software to the
 * public domain worldwide. This software is distributed without any
 * warranty.
 * 
 * You should have received a copy of the CC0 Public Domain Dedication
 * along with this software (see the LICENSE.md file). If not, see
 * <http://creativecommons.org/publicdomain/zero/1.0/>.
 */

apply plugin: 'groovy'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def componentNode = parseComponent(project)
version = componentNode.'@version'
def jarBaseName = componentNode.'@name'
def moquiDir = projectDir.parentFile.parentFile.parentFile
def frameworkDir = file(moquiDir.absolutePath + '/framework')

// to run use "gradle dependencyUpdates"
apply plugin: 'com.github.ben-manes.versions'
buildscript {
  repositories { jcenter() }
  dependencies { classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.0' }
}
dependencyUpdates.resolutionStrategy = { componentSelection { rules -> rules.all { ComponentSelection selection ->
    boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier -> selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/ }
    if (rejected) selection.reject('Release candidate')
} } }

repositories {
    flatDir name: 'localLib', dirs: frameworkDir.absolutePath + '/lib'
    flatDir name: 'librepo', dirs: projectDir.absolutePath + '/librepo'
    jcenter()
    mavenCentral()
}

dependencies {
    compile project(':framework')

    // Apache FOP
    compile('org.apache.xmlgraphics:fop:2.2') { // Apache 2.0
        exclude group: 'xalan'
        exclude group: 'xml-apis' // can't exclude batik-ext but has some same classes as xml-apis so exclude that
    }
    compile ('org.apache.xmlgraphics:batik-transcoder:1.9') {
        exclude group: 'xalan'
    }
    compile ('org.apache.xmlgraphics:batik-constants:1.9')
    compile ('org.apache.xmlgraphics:batik-i18n:1.9')

    // barcode4j as a module to avoid required dependency on ant
    compile module('net.sf.barcode4j:barcode4j:2.1')
    compile module('net.sf.barcode4j:barcode4j-fop-ext:2.1')

    // CSSBox (HTML/CSS to SVG, etc)
    compile ('net.sf.cssbox:cssbox:4.12') { // LGPL <== NOTE!
        exclude group: 'xalan'
        exclude group: 'xml-apis' // can't exclude batik-ext but has some same classes as xml-apis so exclude that
    }
    compile ('org.apache.pdfbox:pdfbox:2.0.5') // Apache 2.0
    // NOTE: this comes from a custom build, see: https://github.com/jonesde/CSSBoxPdf
    compile name: 'cssbox-pdf', version: '1.0-SNAPSHOT' // LGPL <== NOTE!
}

// by default the Java plugin runs test on build, change to not do that (only run test if explicit task)
check.dependsOn.remove(test)
task cleanLib(type: Delete) { delete fileTree(dir: projectDir.absolutePath+'/lib', include: '*') }
clean.dependsOn cleanLib

jar {
    destinationDir = file(projectDir.absolutePath + '/lib')
    baseName = jarBaseName
}
task copyDependencies { doLast {
    copy { from (configurations.runtime - project(':framework').configurations.runtime - project(':framework').jar.archivePath)
        into file(projectDir.absolutePath + '/lib') }
} }
copyDependencies.dependsOn cleanLib
jar.dependsOn copyDependencies
