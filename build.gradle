/*
 * This software is in the public domain under CC0 1.0 Universal plus a 
 * Grant of Patent License.
 * 
 * To the extent possible under law, the author(s) have dedicated all
 * copyright and related and neighboring rights to this software to the
 * public domain worldwide. This software is distributed without any
 * warranty.
 * 
 * You should have received a copy of the CC0 Public Domain Dedication
 * along with this software (see the LICENSE.md file). If not, see
 * <http://creativecommons.org/publicdomain/zero/1.0/>.
 */

apply plugin: 'groovy'

def componentNode = parseComponent(project)
version = componentNode.'@version'
def jarBaseName = componentNode.'@name'
def moquiDir = projectDir.parentFile.parentFile.parentFile
def frameworkDir = file(moquiDir.absolutePath + '/framework')

repositories {
    flatDir name: 'localLib', dirs: frameworkDir.absolutePath + '/lib'
    flatDir name: 'librepo', dirs: projectDir.absolutePath + '/librepo'
    mavenCentral()
}

// Log4J has annotation processors, disable to avoid warning
tasks.withType(JavaCompile) { options.compilerArgs << "-proc:none" }
tasks.withType(GroovyCompile) { options.compilerArgs << "-proc:none" }

dependencies {
    implementation project(':framework')

    // Apache PDFBox
    implementation ('org.apache.pdfbox:pdfbox:3.0.6') // Apache 2.0

    // Apache FOP - Apache 2.0
    implementation ('org.apache.xmlgraphics:fop-core:2.11')
    implementation ('org.apache.xmlgraphics:fop-events:2.11') {
        exclude group: 'org.apache.ant'
    }
    implementation ('org.apache.xmlgraphics:fop-util:2.11')

    // NOTE: Apache Avalon is not a transititive dependency for FOP, however it
    // is loaded indirectly via Java SPI and reflection.
    implementation "org.apache.avalon.framework:avalon-framework-api:4.3.1"
    implementation "org.apache.avalon.framework:avalon-framework-impl:4.3.1"

    implementation('net.sf.barcode4j:barcode4j:2.1') {
        exclude group: 'org.apache.xmlgraphics', module: 'fop'
        exclude group: 'org.apache.ant', module: 'ant'
    }
    implementation('net.sf.barcode4j:barcode4j-fop-ext:2.1') {
        exclude group: 'org.apache.xmlgraphics', module: 'fop'
        exclude group: 'org.apache.ant', module: 'ant'
    }

    // CSSBox (HTML/CSS to SVG, etc)
    implementation ('net.sf.cssbox:cssbox:4.17') { // LGPL <== NOTE!
        exclude group: 'xerces'
        exclude group: 'xml-apis' // Provided in later JDKs; exclude to avoid duplicate DOM/SAX classes
    }
    // NOTE: this comes from a custom build, see: https://github.com/jonesde/CSSBoxPdf
    implementation(':cssbox-pdf:1.0-SNAPSHOT') { // LGPL <== NOTE!
        exclude group: 'xerces'
        exclude group: 'xml-apis'
    }
}

task cleanLib(type: Delete) { delete fileTree(dir: projectDir.absolutePath+'/lib', include: '*') }
clean.dependsOn cleanLib

jar {
    destinationDirectory = file(projectDir.absolutePath + '/lib')
    archiveBaseName = jarBaseName
}
tasks.register('copyDependencies', Copy) {
    dependsOn cleanLib
    from (configurations.runtimeClasspath -
          project(':framework').configurations.runtimeClasspath -
          project(':framework').tasks.named('jar').get().archiveFile.get().asFile)
    into file(projectDir.absolutePath + '/lib')
}
jar.dependsOn copyDependencies
